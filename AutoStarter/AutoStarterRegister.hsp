
#packopt name "AutoStarterRegister"
#packopt hide    1
#packopt orgpath 1

#include "hspext.as"

	sdim list, 64
	sdim StartUpDir, MAX_PATH
	sdim Current   , MAX_PATH
	
	Current    = dirinfo(0)					// カレントディレクトリ
	StartUpDir = dirinfo(0x10007)			// ユーザのスタートアップ
	
	chdir StartUpDir
	dirlist list, "AutoStarter.exe.lnk", 0		// リンクがあるかどうか
	
	if ( stat == 0 ) {						// リンクがない
		dialog "自動実行を設定しますか？", 2, "確認"
		if (stat == 7) { goto *exit }	// no
		
		// ショートカット作成
		LinkName = "AutoStarter.exe"
		fxlink LinkName, dirinfo(1) +"\\AutoStarter.exe"
		if ( stat ) {			// 失敗
			dialog "自動実行の設定に失敗しました。\nAutoStarter が ["+ dirinfo(1) +"\\AutoStarter.exe] \nにあることを確認してください。", 1, "Error"
		} else {
			dialog "自動実行の設定が完了しました。", 0, "Non-Error"
		}
	} else {								// リンクがある
		dialog "自動実行を解除しますか？", 2, "確認"
		if (stat == 7) { goto *exit }	// no
		
		// ショートカット削除
		exdel ""+ StartUpDir +"\\AutoStarter.exe.lnk"
		dialog "自動実行設定を解除しました。", 0, "Non-Error"
	}
	
*exit
	chdir Current	// 戻す
	end : end
	
