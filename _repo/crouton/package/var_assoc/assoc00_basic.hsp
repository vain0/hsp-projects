#include "../crouton.as"

// 連想配列

// ※先に「vector」のサンプルをお読みください。

// 単語：連想配列(assoc)
// このプラグインでは、「文字列を添字とする vector」のこと。

// 関数：assoc(  k0, v0,  k1, v1, ...  )
// 新しい連想配列を作る。
// それは k0, k1, ... という添字を持ち、
// それぞれの内部変数は v0, v1, ... という値を持つ。

	// (朝, おはよう), (夜, こんばんわ) という2つの要素を持つ連想配列
	a = assoc("朝", "おはよう",  "夜", "こんばんわ")
	
// 添字
// 配列変数と同様に、添字 ( ) をつけて要素の値に参照する。
// 詳しくは後述
	
	mes "和語版：朝の挨拶の言葉は：" + a("朝")	//=> おはよう
	mes "和語版：夜の挨拶の言葉は：" + a("夜")	//=> こんばんわ
	
// 添字つきの代入
// 1. 添字が既に存在するなら、内部変数に代入する。
// 2. 添字が存在しないなら、新しい要素を作って、それに代入する。

	a("朝") = "hello"
	a("昼") = "good morning"
	
	mes "英語版：朝の挨拶は「" + a("朝") + "」"
	mes "英語版：昼の挨拶は「" + a("昼") + "」"

// 「内部変数への添字」
// キーの後に続く引数は、そのキーに対応する要素に対する添字として処理される。

	a("列") = vector(0, 1, 2)
	
	repeat 3
		// a("列") の cnt 番目の要素
		n = a("列", cnt)
		
		mes strf("a(\"列\", %d) = %d", cnt, n)
	loop
	
	mes "以上"
	stop

