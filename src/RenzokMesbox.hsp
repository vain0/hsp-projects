

#include "Renzok.hsp"

*main
	gsel 0, -1
	gosub *l_var_init
	gosub *l_init_callback
	gosub *CreateWindow
	gosub *OnRenewal
	
	gsel wID_Main
	onkey gosub *OnKeyLabel
	onexit goto *exit
	
	oncmd gosub *OnCommand,   0x0111			// WM_COMMAND ListBox と EditControl の割り込み
	oncmd gosub *OnResize,    0x0005			// WM_SIZE      サイズ可変用
	oncmd gosub *OnSizing,    0x0214			// WM_SIZING    サイズ固定用
	oncmd gosub *OnMouseMove, 0x0200			// WM_MOUSEMOVE マウス監視用
	oncmd gosub *OnLBtnDown,  0x0201			// WM_LBUTTONDOWN ドラッグ開始検知用
	oncmd gosub *OnLoad,      UWM_LOAD
	oncmd gosub *OnChangePath,UWM_CHANGEPATH
	oncmd gosub *OnRenewal,   UWM_RENEWAL_LIST
	oncmd gosub *OnSplitterMove,UWM_SPLITTERMOVE
	
	gsel wID_Main , 1
	objsel BoxInfo(1)	// カーソルを与える
	
	gsel wID_Main
	stop
	
*exit
	if ( bDragging ) { ClipCursor NULL : bDragging = false }
	end : end
	
//######## メッセージハンドラ ##################################################
*OnKeyLabel
	if ( ginfo_act == wID_Main ) {
		
		// [Ctrl]
		if ( getkey(17) ) {	
			if ( iparam == 'S' ) {
				gosub *Save
				return
			}
		}
		
	}
	return
	
*l_resize_editbox
	MoveWindow  BoxInfo, LBWIDTH + 10,            5, BOXWIDTH, BOXHEIGHT, true
	return
	
*OnCommand
	if ( lparam == LbInfo ) {
		// リストボックスの割り込み
		wNotifyCode = HIWORD(wparam)
		
		switch wNotifyCode
		case 1										// LBN_SELCHANGE
			sendmsg hMain, UWM_LOAD, 0, 0
			swbreak
		case 2										// LBN_WCLICK
			sendmsg hMain, UWM_CHANGEPATH, 0, 0
			swbreak
		swend
		
	} else : if ( lparam == BoxInfo ) {
		// 編集ボックスの割り込み
		wNotifyCode = HIWORD(wparam)
		if ( wNotifyCode == 0x300 ) {		// EN_CHANGE (編集された)
			
			if ( bModified == false ) {				// まだ編集されていなかったときだけ
				sendmsg BoxInfo, 0x00B8, 0, 0		// EM_GETMODIFY (編集フラグを取得)
				if ( stat == false ) { return }
				
				// 編集済み状態にする
				Settitle getpath(filepath, 8) +" *"
				bModified = true
			}
		}
	}
	return
	
*OnLoad
	// 読み込むファイル名を取得
	sendmsg LbInfo, 0x0188, 0, 0 : iList = stat
	sendmsg LbInfo, 0x0189, iList, varptr(stmp)
	
	if ( iList < 0 ) { return }					// 選択アイテムがない場合も無視する
	
	// Ｗクリックが必要なものは無視
	if ( stmp == STR_SEPARATOR ) { return }
	if ( stmp == STR_DIALOG )    { return }		// フォルダ移動か？
	if ( peek(stmp) == '[' )     { return }		// フォルダか？
	
	// ファイルなら
	stmp = path +"\\"+ stmp
	if ( filepath == stmp ) { return }	// すでに開いている
	
	exist stmp
	if ( strsize < 0 ) { return }		// ない ( IsBinFile が調べてくれる )
	if ( strsize >= MAX_TEXTLEN ) {
		EnableBox {"
			ファイルサイズが大きすぎます
			(This file's size is too big to load it.)
			"}+ ( strsize / 1024 ) +{"KB
		"}
		return
		
	} if ( IsBinFile( stmp ) <= 0 ) {	// 0 = Binary : 1 = Text : -1 = Unknown
		EnableBox "(bin file)"
		return
		
	} if ( bEnable ) {
		EnableWindow BoxInfo, true
		bEnable = false
	}
	
	filepath = stmp				// ファイルを変更した
	
;	memexpand buf, strsize + 5
	notesel   buf
	noteload  filepath
	noteunsel
	
	objprm BoxInfo(1), buf
	Settitle getpath(filepath, 8)	// 変更フラグをおろす
	bModified = false
	return
	
*OnChangePath
	// 読み込むファイル名を取得
	sendmsg LbInfo, 0x0188, 0, 0 : iList = stat
	sendmsg LbInfo, 0x0189, iList, varptr(stmp)
	
	if ( iList < 0 ) { return }					// 選択アイテムがない場合も無視する
	
	// セパレータか？
	if ( stmp == STR_SEPARATOR ) { return }
	
	// フォルダ移動か？
	if ( stmp == STR_DIALOG ) {
		foldlg "", path, 0
		if ( stat == false ) {			// 成功したとき
			path = refstr
			sendmsg hMain, UWM_RENEWAL_LIST, 0, 0
		}
		return
	}
	
	// フォルダか？
	if ( peek(stmp) == '[' ) {
		
		if ( stmp == STR_BACK ) {
			// 最後のフォルダ名と \ を取り除く
			     path = getpath(path, 32)
			poke path,   strlen(path) - 1, 0
			
		} else {
			getstr stmp, stmp, 1, ']'			// フォルダ名を取得
			path += "\\"+ stmp
		}
		
		sendmsg hMain, UWM_RENEWAL_LIST, 0, 0
		return
	}
	return
	
*OnRenewal
	// ListBox を再構築する
	sendmsg LbInfo, 0x0184, 0, 0						// LB_RESETCONTENT
;	sendmsg LbInfo, 0x018D, 0x0010, path +"\\*.*"		// LB_DIR (wparam == DDL_DIRECTORY)
	sendmsg LbInfo, 0x000B, false, 0					// WM_SETREDRAW ( 再描画禁止 )
	DirlistEx  filelist, path
	NoteRepeat filelist
		sendmsg LbInfo, 0x0180, 0, nrNote
	NoteLoop
	sendmsg LbInfo, 0x0180, 0, STR_DIALOG				// LB_ADD
	sendmsg LbInfo, 0x000B, true, 0					// WM_SETREDRAW ( 再描画許可 )
	
	return
	
//######## サブルーチン群 ######################################################
*l_var_init_custom
	dim  BoxInfo, 2				// EditBox 〃
	return
	
*l_create_listbox
	font LISTBOX_FONT_FAMILY, LISTBOX_FONT_SIZE
	objmode objmode_usefont, false
	objsize LBWIDTH, LBHEIGHT
	pos 5, 5 : listbox iList, 0, ""
	LbInfo = objinfo(stat, 2), stat
	return
	
*l_create_editbox
	font EDITOR_FONT_FAMILY, EDITOR_FONT_SIZE
	objmode objmode_usefont, false
	pos LBWIDTH + 10, 5 : mesbox buf, BOXWIDTH, BOXHEIGHT, 5, MAX_TEXTLEN
	BoxInfo = objinfo(stat, 2), stat
	return
	
*Save
	if ( filepath == STR_DIALOG ) { return }
	
	if ( peek(filepath) == 0 ) {
		// 選べる
		dialog "*", 17, "保存先"
		if ( stat == false ) {
			objsel BoxInfo(1)
			return
		}
		filepath = refstr
	}
	
	notesel buf
	notesave filepath
	noteunsel
	
	// 編集済みフラグを消す
	bModified = false
	sendmsg BoxInfo, 0x00B9, false, 0
	Settitle getpath(filepath, 8)
	
	return
	
*l_init_callback
	return
	
#deffunc EnableBox str prm_message
	objprm BoxInfo(1), prm_message	// クリア (理由を表示)
	Settitle "編集不可(Enable)"
	EnableWindow BoxInfo, false	// 無効化
	bEnable  = true
	filepath = ""
	return
