
#define global MAX_TEXTLEN 0x1FFFF  // 127KB までサポート

#include "Renzok.hsp"

#include "Mo/cond.as"

*main
	gosub *l_initialize
	stop
	
*exit
	if ( bDragging ) { ClipCursor NULL : bDragging = false }
	end : end
	
//######## メッセージハンドラ ##################################################
*OnKeyLabel
	if ( ginfo_act == wID_Main ) {
		
		// [Ctrl]
		if ( getkey(17) ) {	
			if ( iparam == 'S' ) {
				gosub *Save
				return
			}
		}
		
	}
	return
	
*l_resize_editbox
	MoveWindow  BoxInfo, LBWIDTH + 10,            5, BOXWIDTH, BOXHEIGHT, true
	return
	
*OnCommand
	if ( lparam == LbInfo ) {
		// リストボックスの割り込み
		wNotifyCode = HIWORD(wparam)
		
		switch wNotifyCode
		case 1										// LBN_SELCHANGE
			sendmsg hMain, UWM_LOAD, 0, 0
			swbreak
		case 2										// LBN_WCLICK
			sendmsg hMain, UWM_CHANGEPATH, 0, 0
			swbreak
		swend
		
	} else : if ( lparam == BoxInfo ) {
		// 編集ボックスの割り込み
		wNotifyCode = HIWORD(wparam)
		if ( wNotifyCode == 0x300 ) {		// EN_CHANGE (編集された)
			
			if ( bModified == false ) {				// まだ編集されていなかったときだけ
				sendmsg BoxInfo, 0x00B8, 0, 0		// EM_GETMODIFY (編集フラグを取得)
				if ( stat ) {
					editbox_set_modify_flag true
				}
			}
		}
	}
	return
	
*OnRenewal
	// ListBox を再構築する
	sendmsg LbInfo, 0x0184, 0, 0						// LB_RESETCONTENT
;	sendmsg LbInfo, 0x018D, 0x0010, path +"\\*.*"		// LB_DIR (wparam == DDL_DIRECTORY)
	sendmsg LbInfo, 0x000B, false, 0					// WM_SETREDRAW ( 再描画禁止 )
	DirlistEx  filelist, path
	NoteRepeat filelist
		sendmsg LbInfo, 0x0180, 0, nrNote
	NoteLoop
	sendmsg LbInfo, 0x0180, 0, STR_DIALOG				// LB_ADD
	sendmsg LbInfo, 0x000B, true, 0					// WM_SETREDRAW ( 再描画許可 )
	
	return
	
//######## サブルーチン群 ######################################################
*l_var_init_custom
	dim  BoxInfo, 2				// EditBox 〃
	return
	
*l_create_listbox
	font LISTBOX_FONT_FAMILY, LISTBOX_FONT_SIZE
	objmode objmode_usefont, false
	objsize LBWIDTH, LBHEIGHT
	pos 5, 5 : listbox iList, 0, ""
	LbInfo = objinfo(stat, 2), stat
	return
	
*l_create_editbox
	font EDITOR_FONT_FAMILY, EDITOR_FONT_SIZE
	objmode objmode_usefont, false
	pos LBWIDTH + 10, 5 : mesbox buf, BOXWIDTH, BOXHEIGHT, 5, MAX_TEXTLEN
	BoxInfo = objinfo(stat, 2), stat
	return
	
*l_init_callback
	return

*l_focus_editbox
	objsel BoxInfo(1)	// カーソルを与える
	return
	
*l_editbox_save
	notesel buf
	notesave filepath
	noteunsel
	
	editbox_set_modify_flag false
	return
	
#deffunc editbox_load_text_file str prm_new_file_path

	filepath = prm_new_file_path
	// assert filepath: テキストファイル
	
	notesel   buf
	noteload  filepath
	noteunsel
	
	objprm BoxInfo(1), buf
	editbox_set_modify_flag false
	return
	
// 編集済みフラグを変更する
#deffunc editbox_set_modify_flag int prm_flag
	bModified = prm_flag
	sendmsg BoxInfo, 0x00B9, bModified, 0
	Settitle getpath(filepath, 8) + cond_s(bModified, " *", "")
	return

// 編集ボックスの有効化・無効化
#deffunc editbox_set_enable int prm_enable

	if ( prm_enable ) {
		if ( is_disabled ) {
			EnableWindow BoxInfo, true
			is_disabled = false
		}
		
	} else {
		Settitle "編集不可(Enable)"
		EnableWindow BoxInfo, false
		is_disabled  = true
	}
	return
	
#deffunc editbox_load_error str prm_message

	editbox_set_enable false
	filepath = ""	
	objprm BoxInfo(1), prm_message
	return
