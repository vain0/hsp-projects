#ifndef IG_COMMAND_LINE_TO_ARGV_HSP
#define IG_COMMAND_LINE_TO_ARGV_HSP

/*
%dll
CommandLineToArgv.hsp
%note
参考: GENKI氏作のサンプル <http://codetter.com/?p=819>
%group
コマンドライン解析命令
%port
Win
%*/

#include "kernel32.as"
#include "shell32.as"

#module

/*
%index
CommandLineToArgv
コマンドライン引数を分解する
%prm
argv
argv: コマンドを表す文字列の配列になる変数
return stat: 引数の個数
%inst
dir_cmdline とは異なり、argv(0) に起動されたプログラムの名前が入る。
%*/
#deffunc CommandLineToArgv array argv,  \
	local argc, local p_cmdline_w, local p_argv_w, local argv_w, local wstr
	
	GetCommandLineW
	p_cmdline_w = stat
	
	CommandLineToArgvW p_cmdline_w, varptr(argc)
	p_argv_w = stat
	
	if ( p_argv_w == 0 ) { return 0 }
	
	dupptr argv_w, p_argv_w, argc * 4, vartype("int")
	repeat argc
		dupptr wstr, argv_w(cnt), 256, vartype("str")
		argv(cnt) = cnvwtos(wstr)
	loop
	
	LocalFree p_argv_w
	return argc
#global

#endif
