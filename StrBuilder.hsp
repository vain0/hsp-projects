#ifndef ig_str_builder_hsp
#define ig_str_builder_hsp

#module StrBuilder mString, mStrlen, mCapacity, mExpand

#define BufSize_Default 4096

//------------------------------------------------
// [i] 構築
//------------------------------------------------
#define global StrBuilder_new(%1, %2 = -1, %3 = -1) \
	newmod %1, StrBuilder@, %2, %3

#modinit int p2, int p3,  \
	local defsize, local expandSize
	
	if ( p2 <= 0 ) { defsize = BufSize_Default } else { defsize = p2 }
	if ( p3 <= 0 ) { mExpand = BufSize_Default } else { mExpand = p3 }
	
	mStrlen = 0
	mCapacity = defsize
	sdim mString, mCapacity
	return
	
//------------------------------------------------
// [i] 解体
//------------------------------------------------
#define global StrBuilder_delete(%1) delmod %1

//------------------------------------------------
// 文字列を設定する
//------------------------------------------------
#modfunc StrBuilder_set str string
	StrBuilder_clear thismod
	StrBuilder_append   thismod, string
	return
	
//------------------------------------------------
// 文字列を後ろに追加する
//------------------------------------------------
#modfunc StrBuilder_append str src, int _lenToAppend
	
	if ( _lenToAppend ) { tmpLen = _lenToAppend } else { tmpLen = strlen(src) }
	
	// overflow しないように
	if ( mCapacity <= mStrlen + tmpLen ) {
		StrBuilder_expand_capacity thismod, tmpLen
	}
	
	// 書き込む (poke 側が src を strlen する分無駄がある)
	poke mString, mStrlen, src
	mStrlen += strsize
	assert (strsize == tmpLen)
	return
	
#modfunc StrBuilder_append_v var src, int _lenToAppend
	if ( _lenToAppend ) { tmpLen = _lenToAppend } else { tmpLen = strlen(src) }
	
	if ( mCapacity <= mStrlen + tmpLen ) {
		StrBuilder_expand_capacity thismod, tmpLen
	}
	
	// 書き込む ('\0' を含める)
	memcpy mString, src, tmpLen + 1, mStrlen
	mStrlen += tmpLen
	return
	
// 最後に追加された文字列の長さ
#defcfunc StrBuilder_strsize
	return tmpLen

//------------------------------------------------
// 文字を連結する
//------------------------------------------------
#modfunc StrBuilder_append_char int c
	if ( c == 0 ) { return }
	
	// over-flow しないように
	if ( mCapacity <= mStrlen + 1 ) {
		StrBuilder_expand_capacity thismod, 1
	}
	
	// 書き込む
	wpoke mString, mStrlen, c
	mStrlen ++
	return
	
//------------------------------------------------
// 文字列を後ろから削る
//------------------------------------------------
#modfunc StrBuilder_erase_back int sizeErase
	if ( sizeErase <= 0 ) { return }
	
	mStrlen -= sizeErase
	if ( mStrlen < 0 ) { mStrlen = 0 }
	
	// 終端文字を置く
	poke mString, mStrlen, 0
	
	return
	
//------------------------------------------------
// バッファを確保する
//------------------------------------------------
#modfunc StrBuilder_ensure_capacity int size
	if ( mCapacity < size ) {
		mCapacity = size + mExpand
		memexpand mString, mCapacity
	}
	return
	
#modfunc StrBuilder_expand_capacity int size
	mCapacity += size + mExpand
	memexpand mString, mCapacity
	return
	
//------------------------------------------------
// 文字列を変数バッファに複写する
//------------------------------------------------
#modfunc StrBuilder_copy_to var buf
	if ( vartype( buf ) != vartype("str") ) {
		sdim      buf, mStrlen + 1
	} else {
		memexpand buf, mStrlen + 1
	}
	memcpy buf, mString, mStrlen + 1
	return
	
//------------------------------------------------
// [i] 文字列の長さを返す
//------------------------------------------------
#modcfunc StrBuilder_length
	return mStrlen

//------------------------------------------------
// 確保済みバッファの大きさを返す
//------------------------------------------------
#modcfunc StrBuilder_capacity
	return mCapacity
	
//------------------------------------------------
// 文字列を関数形式で返す(非推奨)
//------------------------------------------------
#modcfunc StrBuilder_str
	return mString
	
#modcfunc StrBuilder_data_ptr
	return varptr(mString)
	
//------------------------------------------------
// [i] 初期化
//------------------------------------------------
#modfunc StrBuilder_clear
	poke mString
	mStrlen = 0
	return
	
//------------------------------------------------
// [i] 連結
//------------------------------------------------
#modfunc StrBuilder_chain var src,  local data, local len
	len = StrBuilder_length(src)
	dupptr data, StrBuilder_data_ptr(src), len + 1, vartype("str")
	StrBuilder_append_v thismod, data, len
	return
	
//------------------------------------------------
// [i] 複写
//------------------------------------------------
#modfunc StrBuilder_copy var src
	StrBuilder_clear thismod
	StrBuilder_chain thismod, src
	return
	
#global

#endif
