// call.hpi - sample 13

#include "call.as"

// any タイプの引数を使う

	call_dec *toString, "any"
	call_dec *f,        "var", "any"
	call_dec *which,    "int", "any", "any"
	
	mes call(*toString, "Hello, world!")
	mes call(*toString, M_PI)
	mes call(*toString, 42)
	
	s = "var swap"
	call *f, a, M_PI     : mes "a := "+ a +";"
	call *f, a, byref(s) : mes "a := "+ a +";\ts := "+ s +";"
	
	repeat 2
		stmp = call( *which, cnt == 0, "'then' value", "'else' value" )
		mes strf( "[%d] %s.", cnt, stmp )
	loop
	
	stop
	
//------------------------------------------------
// 文字列への変換
// 
// @ any 型の受け取りは var または array 。
//------------------------------------------------
#defcfunc lbf_toString var value
*toString
	mes "(vartype: " + vartype(value) + ")"	;arginfo(ARGINFOID_FLAG, 0)
	
	return str(value)
	
//------------------------------------------------
// 代入と交換
// 
// @ 役立たずな命令
//------------------------------------------------
#deffunc lbf_f var lhs, var rhs
*f
	// rhs が参照渡しの場合、交換
	if ( arginfo(ARGINFOID_BYREF, 1) ) {
		tmp = lhs
		lhs = rhs
		rhs = tmp
		
	// rhs が値渡しの場合、代入
	} else {
		lhs = rhs
	}
	
	return
	
//------------------------------------------------
// 条件関数
// 
// @ 実用性が高い……かも。
//------------------------------------------------
#defcfunc lbf_which int b, var valueThen, var valueElse
*which
	if ( b ) {
		return valueThen
	} else {
		return valueElse
	}
	
