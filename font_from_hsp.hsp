#ifndef ig_font_from_hsp_hsp
#define ig_font_from_hsp_hsp

/*
HSP の font 命令を利用してフォントオブジェクトを作成するモジュール

%port
Windows GUI 32bit版限定
%*/

#module __font_from_hsp

#uselib "gdi32.dll"
#func   CreateFontIndirect@__font_from_hsp "CreateFontIndirectA" int
#func   GetObject@__font_from_hsp    "GetObjectA"   int,int,int
#func   DeleteObject@__font_from_hsp "DeleteObject" int

/*
%index
font_get_current_logfont
現在のLOGFONT構造体を取得
%prm
array lf: LOGFONT構造体をコピーする配列変数
%inst
カレントウィンドウに設定されている font の LOGFONT 構造体を lf にコピーする。
%*/
#deffunc font_get_current_logfont array lf,  \
	local bmscr

	dim lf, 15
	mref bmscr, 67
	GetObject bmscr(38), 60, varptr(lf)
	return
	
/*
%index
font_get_current_setting
現在のフォント設定を取得
%prm
face, pt, style
face: フォント名が代入される文字列型変数
pt: フォントサイズが代入される変数
style: フォントスタイルが代入される変数
%inst
font 命令に指定されたパラメータを取得する。
%*/
#deffunc font_get_current_setting var face, var pt, var style,  \
	local lf
	
	if ( vartype(face) != 2 ) { sdim face }
	
	font_get_current_logfont lf
	getstr face, lf(7)
	pt = (lf(0) ^ 0xFFFFFFFF) + 1
	
	style = 0
	style |= (  lf(4) >= 700             ) << 0	// Bold
	style |= ( (lf(5) & 0x000000FF) != 0 ) << 1	// Italic
	style |= ( (lf(5) & 0x0000FF00) != 0 ) << 2	// UnderLine
	style |= ( (lf(5) & 0x00FF0000) != 0 ) << 3	// StrikeLine
	style |= ( (lf(6) & 0x00040000) != 0 ) << 4	// AntiAlias
	return
	
/*
%index
font_from_hsp
フォントオブジェクトの作成
%prm
(face, pt, size)
return: フォントハンドル
%inst
新しいフォントオブジェクトを作成して、そのハンドルを返す。
パラメータは font 命令と同一。

返却されたフォントハンドルは、不必要になった後に必ず font_delete 命令に渡すこと。
%*/
#defcfunc font_from_hsp str face, int pt, int style,  \
	local face_bak, local pt_bak, local style_bak,  \
	local lf, local h_font
	
	// Backup
	font_get_current_setting face_bak, pt_bak, style_bak
	
	// Create new font
	font face, pt, style
	font_get_current_logfont lf
	CreateFontIndirect varptr(lf)
	h_font = stat
	
	// Restore
	font face_bak, pt_bak, style_bak
	return h_font
	
/*
%index
objfont
コントロールのフォントを変更
%prm
h_control, face, pt, style
h_control: 対象のコントロールのハンドル
face, pt, style: font 命令と同じ
return: 作成されたフォントハンドル
%inst
コントロールのフォントを変更する。

返却されたフォントハンドルは、プログラムの終了前に font_delete 命令に渡すこと。
%*/
#define global objfont(%1, %2, %3, %4 = 0, %5 = 1) \
	objfont_ %1, %2, %3, %4, %5

#deffunc objfont_ int h_control, str face, int pt, int style, int refresh,  \
	local h_font
	
	h_font = font_from_hsp(face, pt, style)
	// WM_SETFONT
	sendmsg h_control, 0x0030, h_font, refresh
	return h_font

/*
%index
font_delete
フォントオブジェクトの解放
%prm
h_font
h_font: フォントオブジェクトのハンドル
%*/
#deffunc font_delete int h
	if ( h ) { DeleteObject h }
	return
	
#global

//##############################################################################
//                サンプル・スクリプト
//##############################################################################
//------------------------------------------------
// サンプル 1
//------------------------------------------------
#if 0
	// StaticText Control を作成
	winobj "static", " \n表示するテキスト ", 0, 0x50000000, ginfo_winx, ginfo_winy
	h_static = objinfo_hwnd(stat)
	
	wait 100
	
	// 太字, 斜体
	objfont h_static, msmincho, 16, 1 | 2
	h_font = stat
	
	onexit goto *LOnExit
	stop
	
*LOnExit
	// プログラム終了時にフォントオブジェクトを解放
	font_delete h_font
	end
	
#endif

//------------------------------------------------
// サンプル 2
//------------------------------------------------
#if 0

	repeat 2
		screen cnt, 320, 240, , cnt * 320, 0
		title "Window #" + cnt
		
		// ウィンドウにそれぞれ異なるフォントを設定
		if (cnt == 0) {
			font msgothic, 12, 1 // 太字
		} else {
			font "游ゴシック", 14, 2 // 斜体
		}
	loop
	
	onclick gosub *LFontSwap
	
	repeat
		repeat 2
			gsel cnt
			redraw 0
			hsvcolor ,, 255 : boxf
			color
			
			pos 0, 0
			mes "画面をクリックするとフォントが交換されます"
			mes strf("これはウィンドウID %d", cnt)
			redraw
		loop
		
		await 17
	loop
	stop
	
*LFontSwap
	sdim face, , 2
	repeat 2
		gsel cnt
		font_get_current_setting face(cnt), pt(cnt), style(cnt)
	loop
	repeat 2
		gsel cnt
		font face(1 - cnt), pt(1 - cnt), style(1 - cnt)
	loop
	return
	
#endif

#endif
